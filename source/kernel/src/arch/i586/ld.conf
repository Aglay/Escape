OUTPUT_FORMAT("elf32-i386")
ENTRY(loader)

SECTIONS
{
	. = 0x100000;

	.init :
	{
		*(.init)
	}

	. += 0xC0000000;
	
	.text : AT(ADDR(.text) - 0xC0000000)
	{
		_btext = .;
		*(.text*)
	}

    .init_array : AT (ADDR (.init_array) - 0xC0000000)
    {
        PROVIDE (CTORS_BEGIN = .);
        KEEP (*(.init_array .ctors))
        PROVIDE (CTORS_END = .);
    }
	
	.data ALIGN (4096) : AT(ADDR(.data) - 0xC0000000)
	{
		*(.data)
		*(.rodata*)
	}

	.bss ALIGN (4096) : AT(ADDR(.bss) - 0xC0000000)
	{
		*(COMMON*)
		*(.bss*)
		_ebss = .;
	}
}
