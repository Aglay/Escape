# -*- Mode: Python -*-

Import('env')

myenv = env.Clone()
builddir = Dir(myenv['BUILDDIR']).abspath + '/lib/shell'
myenv.Append(
	CFLAGS = ' -DYYERROR_VERBOSE -I lib/shell -I ' + builddir,
	LEXFLAGS = '--nounistd',
	YACCFLAGS = '-t -d'
)

lexer = myenv.CFile(target = 'lex.c', source = 'script.l')
parser = myenv.CFile(target = 'parser', source = 'script.y')

cfiles = [f for f in Glob('*.c') if 'lex.c' not in str(f) and 'parser.c' not in str(f)]
cfiles += [f for f in Glob('*/*.c') if 'lex.c' not in str(f) and 'parser.c' not in str(f)]
myenv.Depends(cfiles, [lexer, parser])

myenv.EscapeLib(myenv, target = 'shell', source = cfiles + [lexer, parser[0]])
