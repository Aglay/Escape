# -*- Mode: Python -*-

import sys, os
sys.path.insert(0, 'tools')
import install

target = os.environ.get('ESC_TARGET') or 'x86_64'
if target == 'i586':
	crossver = '4.6.3'
elif target == 'eco32':
	crossver = '4.4.3'
elif target == 'mmix':
	crossver = '4.6.3'
else:
	print "Please define ESC_TARGET to i586, eco32 or mmix first!"
	Exit(1)
cross = target + '-elf-escape'
crossdir = '/opt/escape-cross-' + target

hostenv = Environment(
	ENV = os.environ,
	CXXFLAGS = '-Wall -Wextra -ansi -g'
)
env = Environment(
	CFLAGS = '-Wall -ansi -Wextra -Wshadow -Wpointer-arith -Wcast-align -Wwrite-strings'
			+ ' -Wmissing-prototypes -Wmissing-declarations -Wnested-externs -Winline'
			+ ' -Wno-long-long -Wstrict-prototypes -fms-extensions',
	CXXFLAGS = '-Wall -ansi -std=c++0x -Wextra -Wshadow -Wpointer-arith -Wcast-align -Wwrite-strings'
			+ ' -Wmissing-declarations -Wno-long-long',
	ASFLAGS = '-Wa,--warn',
	ENV = {'PATH' : crossdir + "/bin:" + os.environ['PATH']},
	CPPPATH = ['#include', '#include/cpp'],
	CXX = cross + '-g++',
	LD = cross + '-ld',
	AS = cross + '-as',
	CC = cross + '-gcc',
	AR = cross + '-ar',
	RANLIB = cross + '-ranlib',
)

linktype = 'static'
if target == 'i586':
	linktype = os.environ.get('ESC_LINKTYPE')
	if linktype != 'static':
		linktype = 'dynamic'
	gcclinktype = os.environ.get('ESC_GCCLINKTYPE')
	if gcclinktype == 'static':
		env.Append(LINKFLAGS = ' -static-libgcc')

btype = os.environ.get('ESC_BUILD')
if btype == 'debug':
	env.Append(
		CFLAGS = ' -O0 -fno-builtin -ggdb -D LOGSERIAL',
		CXXFLAGS = ' -O0 -fno-inline -ggdb'
	)
else:
	env.Append(
		CFLAGS = ' -O3 -fno-builtin -DNDEBUG -fno-omit-frame-pointer',
		CXXFLAGS = ' -O3 -DNDEBUG -fno-omit-frame-pointer'
	)
	btype = 'release'
builddir = 'build/' + target + '-' + btype

if target == 'mmix':
	env.Append(
		# atm we have to disable base-addresses, because the mmix-gcc-port seems to be unable to
		# handle it correctly
		CFLAGS = ' -mno-base-addresses',
		CXXFLAGS = ' -mno-base-addresses',
		# do not warn if instructions are extended to setl,incml,...
		ASFLAGS = ' -Wa,-x'
	)

verbose = os.environ.get('ESC_VERBOSE',0)
if int(verbose) == 0:
	hostenv['ASCOMSTR'] = env['ASCOMSTR'] = "AS $TARGET"
	hostenv['ASPPCOMSTR'] = env['ASPPCOMSTR'] = "ASPP $TARGET"
	hostenv['CCCOMSTR'] = env['CCCOMSTR'] = "CC $TARGET"
	hostenv['SHCCCOMSTR'] = env['SHCCCOMSTR'] = "SHCC $TARGET"
	hostenv['CXXCOMSTR'] = env['CXXCOMSTR'] = "CXX $TARGET"
	hostenv['SHCXXCOMSTR'] = env['SHCXXCOMSTR'] = "SHCXX $TARGET"
	hostenv['LINKCOMSTR'] = env['LINKCOMSTR'] = "LD $TARGET"
	hostenv['SHLINKCOMSTR'] = env['SHLINKCOMSTR'] = "SHLD $TARGET"
	hostenv['ARCOMSTR'] = env['ARCOMSTR'] = "AR $TARGET"
	hostenv['RANLIBCOMSTR'] = env['RANLIBCOMSTR'] = "RANLIB $TARGET"

env.Append(
	ARCH = target,
	LINKTYPE = linktype,
	CROSS = cross,
	CROSSDIR = Dir(crossdir),
	BUILDDIR = Dir(builddir),
	DISTDIR = Dir(builddir + '/dist'),
	BINDIR = Dir(builddir + '/bin'),
	MAPDIR = Dir(builddir + '/map'),
	SYSLIBPATH = Dir(crossdir + '/lib'),
	SYSGCCLIBPATH = Dir(crossdir + '/lib/gcc/' + cross + '/' + crossver),
	SYSSHGCCPATH = Dir(crossdir + '/' + cross + '/lib')
)
env['LIBPATH'] = [env['BINDIR'], env['SYSLIBPATH']]
hostenv['BINDIR'] = env['BINDIR']
link_addr = 0x1000

def SetLibDeps(env, lib, libs):
	for l in libs:
		if l != 'gcc':
			env.Depends(lib, '$BINDIR/lib' + l + '.so')
	if str(lib[0]) != 'libc.so' and str(lib[0]) != 'libstdc++.so':
		env.Depends(lib, '$BINDIR/libc.so')
		env.Depends(lib, '$BINDIR/libstdc++.so')

def SetProgDeps(env, prog, static, libs, cpp):
	crtsuffix = '.o' if static else 'S.o'
	libsuffix = '.a' if static else '.so'
	for l in libs:
		if l != 'gcc':
			env.Depends(prog, '$BINDIR/lib' + l + libsuffix)
	env.Depends(prog, '$SYSGCCLIBPATH/crt0' + crtsuffix)
	env.Depends(prog, '$SYSGCCLIBPATH/crt1' + crtsuffix)
	env.Depends(prog, '$SYSGCCLIBPATH/crtn' + crtsuffix)
	env.Depends(prog, '$BINDIR/libc' + libsuffix)
	env.Depends(prog, '$BINDIR/libg' + libsuffix)
	env.Depends(prog, '$BINDIR/libm' + libsuffix)
	if cpp:
		env.Depends(prog, '$BINDIR/libstdc++' + libsuffix)
		if static:
			env.Depends(prog, '$SYSGCCLIBPATH/libgcc.a')
		else:
			env.Depends(prog, '$SYSSHGCCPATH/libgcc_s.so.1')
		env.Depends(prog, '$SYSLIBPATH/libsupc++.a')

def SetLinkAddr(env, static):
	global link_addr
	# TODO this doesn't seem to work with MMIX. why?
	if env['ARCH'] == 'i586':
		if static:
			env.Append(LINKFLAGS = ' -Wl,--section-start=.init=' + ("0x%x" % link_addr))
		else:
			env.Append(LINKFLAGS = ' -Wl,--section-start=.interp=' + ("0x%x" % link_addr))
		link_addr += 0x20000

def EscapeMap(env, target, source):
	map = env.Command(target, source, 'tools/createmap-$ARCH $SOURCE > $TARGET')
	env.Install('$MAPDIR', map)

def EscapeDump(env, target, source):
	dump = env.Command(target, source, '$BINDIR/hostdump $SOURCE > $TARGET')
	env.Depends(dump, '$BINDIR/hostdump')
	env.Install('$BUILDDIR', dump)

def EscapeCProg(env, prefix, target, source, force_static = False, var_addr = True, LIBS = []):
	myenv = env.Clone()
	static = force_static or env['LINKTYPE'] == 'static'
	if static:
		myenv.Append(LINKFLAGS = ' -static -Wl,-static')
	if var_addr:
		SetLinkAddr(myenv, static)
	prog = myenv.Program(target, source, LIBS = LIBS)
	SetProgDeps(myenv, prog, static, LIBS, False)
	myenv.EscapeMap(target + '.map', prog)
	if prefix != '':
		myenv.Install('$DISTDIR/' + prefix, prog)
		myenv.Symlink('$BINDIR/' + target, prog)
	return prog

def EscapeCXXProg(env, prefix, target, source, force_static = False, LIBS = []):
	myenv = env.Clone()
	static = force_static or env['LINKTYPE'] == 'static'
	if static:
		myenv.Append(LINKFLAGS = ' -static -Wl,-static')
	SetLinkAddr(myenv, static)
	prog = myenv.Program(target, source, LIBS = ['stdc++', 'supc++'] + LIBS)
	SetProgDeps(myenv, prog, static, LIBS, True)
	myenv.EscapeMap(target + '.map', prog)
	myenv.Install('$DISTDIR/' + prefix, prog)
	myenv.Symlink('$BINDIR/' + target, prog)
	return prog

def EscapeLib(env, target, source, LIBS = []):
	myenv = env.Clone()
	stlib = myenv.StaticLibrary(target, source)
	env.Install('$BINDIR', stlib)
	if env['ARCH'] == 'i586':
		shenv = myenv.Clone()
		shenv.Append(LINKFLAGS = ' -Wl,-shared -Wl,-soname,lib' + target + '.so')
		shlib = shenv.SharedLibrary(target, source, LIBS = LIBS)
		SetLibDeps(env, shlib, LIBS)
		env.Install('$BINDIR', shlib)
		env.Install('$DISTDIR/lib', shlib)
        env.EscapeMap('lib' + target + '.map', shlib)

symlink = Builder(action = 'ln -sf ${SOURCE.abspath} $TARGET')
env.Append(BUILDERS = {'Symlink' : symlink})
env.AddMethod(EscapeMap)
env.AddMethod(EscapeDump)
env.AddMethod(EscapeCProg)
env.AddMethod(EscapeCXXProg)
env.AddMethod(EscapeLib)
env.AddMethod(install.InstallFiles)

hostenv.SConscript('tools/SConscript', 'hostenv',
				   variant_dir = builddir + '/tools', duplicate = 0)

for d in ['dist', 'bootloader', 'lib', 'drivers', 'user', 'kernel']:
	env.SConscript(d + '/SConscript', 'env',
				   variant_dir = builddir + '/' + d, duplicate = 0)

# EOF
