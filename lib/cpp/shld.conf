OUTPUT_FORMAT("elf32-i386")
ENTRY(init)

SECTIONS
{
	.text 0x00000000 :
	{
		*(.text.unlikely .text.*_unlikely)
		*(.text .stub .text.* .gnu.linkonce.t.*)
	}
	
	.rel.dyn        :
	{
		*(.rel.init)
		*(.rel.text .rel.text.* .rel.gnu.linkonce.t.*)
		*(.rel.fini)
		*(.rel.rodata .rel.rodata.* .rel.gnu.linkonce.r.*)
		*(.rel.data.rel.ro* .rel.gnu.linkonce.d.rel.ro.*)
		*(.rel.data .rel.data.* .rel.gnu.linkonce.d.*)
		*(.rel.tdata .rel.tdata.* .rel.gnu.linkonce.td.*)
		*(.rel.tbss .rel.tbss.* .rel.gnu.linkonce.tb.*)
		*(.rel.ctors)
		*(.rel.dtors)
		*(.rel.got)
		*(.rel.bss .rel.bss.* .rel.gnu.linkonce.b.*)
		*(.rel.ifunc)
	}
	.rel.plt        :
	{
		*(.rel.plt)
		*(.rel.iplt)
	}

	.rodata ALIGN (4096) :
	{
		*(.rodata .rodata.* .gnu.linkonce.r.*)
	}

	.bss ALIGN (4096) :
	{
		*(.dynbss)
		*(.bss .bss.* .gnu.linkonce.b.*)
		*(COMMON*)
	}

	.data ALIGN (4096) :
	{
		*(.data .data.* .gnu.linkonce.d.*)
		. = ALIGN(4);
		__libcpp_constr_start = .;
		*(.ctors)
		__libcpp_constr_end = .;
	}
	
	.tdata :
	{
		*(.tdata .tdata.* .gnu.linkonce.td.*)
	}
	.tbss :
	{
		*(.tbss .tbss.* .gnu.linkonce.tb.*) *(.tcommon)
	}
}
